apply plugin: 'com.android.application'
apply plugin: 'img-optimizer'
apply plugin: 'com.yanzhenjie.andserver'
apply plugin: 'org.greenrobot.greendao'

//打包时，记得设置true启用
if (isNeedPackage.toBoolean() && isUseBooster.toBoolean()) {
    apply plugin: 'com.didiglobal.booster'
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.xuexiang.templateandserver"
        minSdkVersion 17
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    signingConfigs {
        if (isNeedPackage.toBoolean()) {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }

        debug {
            storeFile file("./debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release

                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                def appID = properties.getProperty("APP_ID_UMENG")
                if (appID != null) {
                    buildConfigField "String", "APP_ID_UMENG", appID
                } else {
                    buildConfigField "String", "APP_ID_UMENG", '""'
                }
            } else {
                signingConfig signingConfigs.debug
                buildConfigField "String", "APP_ID_UMENG", '""'
            }
        }

        debug {
            debuggable true
            minifyEnabled false

            signingConfig signingConfigs.debug
            buildConfigField "String", "APP_ID_UMENG", '""'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    greendao{
        schemaVersion 1 //数据库版本号
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso.core

    //AndServer
    implementation 'javax.activation:javax.activation-api:1.2.0'
    implementation 'com.yanzhenjie.andserver:api:2.1.9'
    annotationProcessor 'com.yanzhenjie.andserver:processor:2.1.9'

    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    //数据库 greendao
    implementation 'org.greenrobot:greendao:3.3.0'


    implementation 'com.github.xuexiangjys:rxutil2:1.2.1'

    implementation deps.androidx.recyclerview

    //下拉刷新
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-layout:1.1.5'

    implementation deps.androidx.multidex
    //屏幕适配AutoSize
    implementation 'me.jessyan:autosize:1.1.2'

}

//x-library依赖脚本
apply from: 'x-library-server.gradle'

